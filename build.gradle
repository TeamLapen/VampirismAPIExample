buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}



apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'eclipse'


configurations {
    optionalRuntime
    runtime.extendsFrom optionalRuntime // optionalRuntime dependencies are downloaded but not exported to POM
}

version = "1.0" // Your mod version
group = "de.maxanier.vampirismapiexample" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "vampirism-api-example"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

repositories {
    //Maven repo for Vampirism
    maven {
        name = "MaxanierMaven"
        url = "https://maxanier.de/maven2"
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
}
dependencies {
    minecraft([
            group: "net.minecraftforge",
            name: "forge",
            version: "${project.minecraft_version}-${project.forge_version}"
    ])
    //compile against the Vampirism API
    compileOnly fg.deobf("de.teamlapen.vampirism:Vampirism:${minecraft_version}-${vampirism_version}:api")
    // Version is taken from gradle.properties
    //at runtime (in your development environment) use the full Vampirism jar
    runtimeOnly fg.deobf("de.teamlapen.vampirism:Vampirism:${minecraft_version}-${vampirism_version}")


    //runtimeOnly fg.deobf("de.maxanier.guideapi:Guide-API-VP:1.14.4-2.2.0")
}


minecraft {
    mappings channel: "${project.mcp_channel}".toString(), version: "${project.mcp_mappings}".toString()

    runs {
        client {
            taskName 'runClient1'
            main 'net.minecraftforge.userdev.LaunchTesting'
            property 'forge.logging.console.level', 'debug'
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            workingDirectory project.file('run/client1').canonicalPath
            
            mods {
                "vampirism-api-example" {
                    source sourceSets.main
                }
            }

        }
        server = {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            workingDirectory project.file('run/server').canonicalPath

            mods {
                'vampirism-api-example' {
                    source sourceSets.main
                }


            }
        }
    }

}


processResources {

    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'

        expand 'version': project.mod_version, 'mcversion': minecraft_version, 'forge_version': forge_version
    }



    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
    outputs.upToDateWhen { false }

    exclude '**/Thumbs.db'
}

